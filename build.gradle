buildscript {
	ext {
		springBootVersion = '2.4.6'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'war'
	id 'checkstyle'
	id 'jacoco'
	id "net.researchgate.release" version "2.8.1"
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.65"
	id "com.github.node-gradle.node" version "3.0.1"
}

group = 'com.dawn.jat'
sourceCompatibility = '1.11'

configurations {
	developmentOnly
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
//	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-test'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	compile "io.springfox:springfox-swagger2:2.9.2"
	compile 'io.springfox:springfox-swagger-ui:2.10.5'
	compile 'org.modelmapper:modelmapper:2.3.9'
}

/**
 * Test using Junit Jupiter.
 * After the test, executing Jacoco generating report.
 */
test {
	useJUnitPlatform()
	finalizedBy(jacocoTestReport)
}

/**
 * Checkstyle to ensure code following proper format.
 */
checkstyle {
	toolVersion '8.27'
	configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
}
checkstyleMain {
	source ='src/main/java'
}
checkstyleTest {
	source ='src/test/java'
}

tasks.withType(Checkstyle) {
	reports {
		html.enabled true
		html.destination rootProject.file("build/reports/checkstyle.html")
	}
}

/**
 * Jacoco to generate Coverage report for Java code.
 */
jacoco {
	toolVersion = "0.8.5"
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
		html.destination file("${buildDir}/reports/jacoco/test")
	}
}

//jacocoTestCoverageVerification {
//	violationRules {
//		rule {
//			limit {
//				minimum = 0.1
//			}
//		}
//
//		rule {
//			element = 'CLASS'
//			includes = ['com.dawn.jat.*']
//			excludes = [
//					'*.entity.*',
//					'*.repository.*',
//					'*.dto.*',
//					'com.dawn.jat.illuminati.ServletInitializer',
//					'com.dawn.jat.illuminati.IlluminatiApplication',
//					'com.dawn.jat.illuminati.AppConfiguration'
//			]
//			limit {
//				counter = 'LINE'
//				value = 'TOTALCOUNT'
//				minimum = 0.1
//			}
//		}
//	}
//}

/**
 * Changelog setup
 */
task generateGitChangelogMinimal(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
	file = new File("CHANGELOG.md");
}

sourceSets {
	main.java.srcDirs = ['src/main/java']
}

/**
 * Our setup are Spring MVC + React App.
 * So we need to exclusively build React App to Static Resource.
 * Then we have to include React build to Spring build before War is create.
 */
node {
	// Version of node to use.
	version = '12.14.1'
	// Enabled the automatic download.
	download = true
}

task yarnInstall(type: YarnTask) {
	workingDir = file('./cms')
}

task buildCms(type: YarnTask) {
	dependsOn yarnInstall
	// add the express package only
	args = ['build']
	workingDir = file('./cms')
}


task moveCmsTemplate(type: Copy) {
	dependsOn buildCms
	from file("./cms/build/")
	include "index.html"
	into file("$buildDir/resources/main/templates/cms")
}

task moveStaticAsset(type: Copy) {
	dependsOn buildCms
	from file("./cms/build/")
	include "**/*"
	exclude "index.html"
	into file("$buildDir/resources/main/static/")
}

bootWar {
	dependsOn moveStaticAsset
	dependsOn moveCmsTemplate
}

bootRun {
	dependsOn moveStaticAsset
	dependsOn moveCmsTemplate
	classpath = sourceSets.main.runtimeClasspath + configurations.developmentOnly
}
